/*
Copyright 2020 Erwan Mahe (github.com/erwanM974)

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/


WHITESPACE = _{ " " | "\r\n" | "\n" | "\t" | "\r" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

HIBOU_LABEL = @{ LETTER ~ (LETTER | ASCII_DIGIT | "_")* }
HIBOU_LABEL_LIST = { "(" ~ HIBOU_LABEL ~ ("," ~ HIBOU_LABEL)* ~ ")" }

HIBOU_true = { "⊤" | "true" | "True" | "TRUE" }
HIBOU_false = { "⊥" | "false" | "False" | "FALSE" }

HIBOU_none = { "none" | "None" | "NONE" }
// ***


GRAPHIC_LOGGER_format_png = { "png" }
GRAPHIC_LOGGER_format_svg = { "svg" }
GRAPHIC_LOGGER_format = _{ ("format" ~ "=")? ~ (GRAPHIC_LOGGER_format_png | GRAPHIC_LOGGER_format_svg) }

GRAPHIC_LOGGER_layout_horizontal = { "horizontal" }
GRAPHIC_LOGGER_layout_vertical = { "vertical" }
GRAPHIC_LOGGER_layout = _{ ("layout" ~ "=")? ~ (GRAPHIC_LOGGER_layout_horizontal | GRAPHIC_LOGGER_layout_vertical) }

GRAPHIC_LOGGER_draw_sequence_diagram = { "draw_sd" ~ "=" ~ (HIBOU_true | HIBOU_false) }
GRAPHIC_LOGGER_draw_term_tree = { "draw_tt" ~ "=" ~ (HIBOU_true | HIBOU_false) }

GRAPHIC_LOGGER_draw_sub_processes = { "draw_sub_processes" ~ "=" ~ (HIBOU_true | HIBOU_false) }
GRAPHIC_LOGGER_draw_legend = { "draw_legend" ~ "=" ~ (HIBOU_true | HIBOU_false) }

GRAPHIC_LOGGER_parent_folder = { ("parent"|"parent_folder"|"folder") ~ "=" ~ HIBOU_LABEL }
GRAPHIC_LOGGER_output_file = { ("output"|"name") ~ "=" ~ HIBOU_LABEL }

GRAPHIC_LOGGER_opt = _{ GRAPHIC_LOGGER_format
                        | GRAPHIC_LOGGER_layout
                        | GRAPHIC_LOGGER_draw_sequence_diagram
                        | GRAPHIC_LOGGER_draw_term_tree
                        | GRAPHIC_LOGGER_draw_sub_processes
                        | GRAPHIC_LOGGER_draw_legend
                        | GRAPHIC_LOGGER_parent_folder
                        | GRAPHIC_LOGGER_output_file }

GRAPHIC_LOGGER_opts = { "[" ~ GRAPHIC_LOGGER_opt ~ ("," ~ GRAPHIC_LOGGER_opt)* ~ (",")? ~ "]" }
OPTION_GRAPHIC_LOGGER = { "graphic" ~ GRAPHIC_LOGGER_opts? }


// ***


TRACEGEN_LOGGER_terminal = { "terminal" }
TRACEGEN_LOGGER_accepted = { "accepted" }
TRACEGEN_LOGGER_depth = { "exact depth" ~ ARITH_INTEGER }
TRACEGEN_LOGGER_modulo = { "modulo depth" ~ ARITH_INTEGER }
TRACEGEN_LOGGER_gen_kind = _{ "generation" ~ "=" ~ ( TRACEGEN_LOGGER_terminal | TRACEGEN_LOGGER_accepted | TRACEGEN_LOGGER_depth | TRACEGEN_LOGGER_modulo ) }

TRACEGEN_LOGGER_partition_discrete = { "discrete" }
TRACEGEN_LOGGER_partition_trivial = { "trivial" }
TRACEGEN_LOGGER_partition_specific = { "{" ~ HIBOU_LABEL_LIST ~ ("," ~ HIBOU_LABEL_LIST)*  ~ "}" }
TRACEGEN_LOGGER_partition = _{ "partition" ~ "=" ~ (TRACEGEN_LOGGER_partition_discrete | TRACEGEN_LOGGER_partition_trivial | TRACEGEN_LOGGER_partition_specific) }

TRACEGEN_LOGGER_parent_folder = { ("parent"|"parent_folder"|"folder") ~ "=" ~ HIBOU_LABEL }
TRACEGEN_LOGGER_trace_prefix = { ("name_prefix"|"trace_prefix"|"prefix") ~ "=" ~ HIBOU_LABEL }

TRACEGEN_LOGGER_opt = _{ TRACEGEN_LOGGER_gen_kind
                        | TRACEGEN_LOGGER_partition
                        | TRACEGEN_LOGGER_parent_folder
                        | TRACEGEN_LOGGER_trace_prefix }

TRACEGEN_LOGGER_opts = { "[" ~ TRACEGEN_LOGGER_opt ~ ("," ~ TRACEGEN_LOGGER_opt)* ~ (",")? ~ "]" }
OPTION_TRACEGEN_LOGGER = { "tracegen" ~ TRACEGEN_LOGGER_opts? }

OPTION_NFAIT_LOGGER = { "nfa" | "nfait" }

// ***


OPTION_LOGGER_KIND = _{ OPTION_GRAPHIC_LOGGER | OPTION_TRACEGEN_LOGGER | OPTION_NFAIT_LOGGER }
OPTION_LOGGER_DECL = { "loggers" ~ "=" ~ "[" ~ OPTION_LOGGER_KIND ~ ("," ~ OPTION_LOGGER_KIND)* ~ (",")? ~ "]" }
// ***********************************************
ARITH_INTEGER = { "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
// ***********************************************
OPTION_PRIORITY_emission = { "emission" }
OPTION_PRIORITY_reception = { "reception" }
OPTION_PRIORITY_loop = { "loop" }
OPTION_PRIORITY_multi_rdv = { "multi_rdv" }
// ***
OPTION_PRIORITY_elim = { "elim" }
OPTION_PRIORITY_simu = { "simu" }
// ***
OPTION_PRIORITY_simpl = { "simpl" }
OPTION_PRIORITY_flush = { "flush" }
OPTION_PRIORITY_invert = { "invert" }
OPTION_PRIORITY_deduplicate = { "deduplicate" }
OPTION_PRIORITY_factorize = { "factorize" }
OPTION_PRIORITY_defactorize = { "defactorize" }
// ***
OPTION_PRIORITY_KIND = _{ OPTION_PRIORITY_emission
                            | OPTION_PRIORITY_reception
                            | OPTION_PRIORITY_loop
                            | OPTION_PRIORITY_multi_rdv
                            | OPTION_PRIORITY_elim
                            | OPTION_PRIORITY_simu
                            | OPTION_PRIORITY_simpl
                            | OPTION_PRIORITY_flush
                            | OPTION_PRIORITY_invert
                            | OPTION_PRIORITY_deduplicate
                            | OPTION_PRIORITY_factorize
                            | OPTION_PRIORITY_defactorize }
OPTION_PRIORITY_LEVEL = { ARITH_INTEGER | ("-" ~ ARITH_INTEGER ) }
OPTION_PRIORITY_RANDOMIZE = { "randomize" ~ "=" ~ (HIBOU_true | HIBOU_false) }
OPTION_PRIORITY_SPECIFIC_elt = { OPTION_PRIORITY_KIND ~ "=" ~ OPTION_PRIORITY_LEVEL }
OPTION_PRIORITY_elt = _{ OPTION_PRIORITY_RANDOMIZE | OPTION_PRIORITY_SPECIFIC_elt }
OPTION_PRIORITY = { "[" ~ OPTION_PRIORITY_elt ~ ("," ~ OPTION_PRIORITY_elt)* ~ (",")? ~ "]" }
// ***********************************************
OPTION_STRATEGY_BFS = { "BreadthFS" | "Breadth First Search" | "BFS" }
OPTION_STRATEGY_DFS = { "DepthFS" | "Depth First Search" | "DFS" }
OPTION_STRATEGY_HCS = { "HighCoverageS" | "High Coverage Search" | "HCS" }
OPTION_STRATEGY_KIND = _{ OPTION_STRATEGY_BFS | OPTION_STRATEGY_DFS | OPTION_STRATEGY_HCS }
OPTION_STRATEGY_DECL = { "strategy" ~ "=" ~ OPTION_STRATEGY_KIND }
// ***********************************************
OPTION_FILTER_MAX_DEPTH = { ("max_depth"|"max" ~ "depth") ~ "=" ~ ARITH_INTEGER }
OPTION_FILTER_MAX_LOOP_DEPTH = { ("max_loop_depth"|"max" ~ "loop" ~ "depth") ~ "=" ~ ARITH_INTEGER }
OPTION_FILTER_MAX_NODE_NUMBER = { ("max_node_number"|"max" ~ "node" ~ "number") ~ "=" ~ ARITH_INTEGER }
OPTION_FILTER = _{ OPTION_FILTER_MAX_DEPTH | OPTION_FILTER_MAX_LOOP_DEPTH | OPTION_FILTER_MAX_NODE_NUMBER }
OPTION_FILTERS_DECL = { "filters" ~ "=" ~ "[" ~ OPTION_FILTER ~ ("," ~ OPTION_FILTER)* ~ (",")? ~ "]" }
// ***********************************************
OPTION_PRIORITIES_DECL = { "priorities" ~ "=" ~ OPTION_PRIORITY }
// ***********************************************
OPTION_MEMOIZE = { "memoize" ~ "=" ~ (HIBOU_true | HIBOU_false) }
// ***********************************************
GENERAL_OPTION_DECL  = _{ OPTION_LOGGER_DECL
                          | OPTION_STRATEGY_DECL
                          | OPTION_FILTERS_DECL
                          | OPTION_PRIORITIES_DECL
                          | OPTION_MEMOIZE }
// ***********************************************
EXPLORE_OPTION_DECL = _{ GENERAL_OPTION_DECL }
EXPLORE_OPTION_SECTION = { "@explore_option" ~ "{" ~
                               	EXPLORE_OPTION_DECL ~ (";" ~ EXPLORE_OPTION_DECL)* ~ (";")?
                                   ~ "}" }



// ***********************************************


OPTION_ANA_KIND_accept = { "accept" }
OPTION_ANA_KIND_prefix = { "prefix" }
OPTION_ANA_KIND_eliminate = { "eliminate" }
// ***
OPTION_ANA_SIMULATE_CONFIG_crit_num = { "num" ~ "=" ~ ARITH_INTEGER }
OPTION_ANA_SIMULATE_CONFIG_crit_maxnum = { "max_num" | "max"~"num" }
OPTION_ANA_SIMULATE_CONFIG_crit_maxdepth = { "max_depth" | "max"~"depth" }

// ***
OPTION_ANA_SIMULATE_CONFIG_loop = { "loop" ~ ( OPTION_ANA_SIMULATE_CONFIG_crit_num
                                                | OPTION_ANA_SIMULATE_CONFIG_crit_maxnum
                                                | OPTION_ANA_SIMULATE_CONFIG_crit_maxdepth
                                                | HIBOU_none ) }
OPTION_ANA_SIMULATE_CONFIG_act = { "act" ~ ( OPTION_ANA_SIMULATE_CONFIG_crit_num
                                                | OPTION_ANA_SIMULATE_CONFIG_crit_maxnum
                                                | HIBOU_none) }
// ***
OPTION_ANA_SIMULATE_CONFIG_simbefore = { ("slice" | "before" | "preamble") ~ "=" ~ (HIBOU_true | HIBOU_false) }
OPTION_ANA_SIMULATE_CONFIG_multiply_by_mu_length = { ("multiply" | "resized" | "dependent") ~ "=" ~ (HIBOU_true | HIBOU_false) }
OPTION_ANA_SIMULATE_CONFIG_reset = { ("reset" | "reinitialize") ~ "=" ~ (HIBOU_true | HIBOU_false) }
OPTION_ANA_SIMULATE_CONFIG_opt = _{ OPTION_ANA_SIMULATE_CONFIG_simbefore
                                    | OPTION_ANA_SIMULATE_CONFIG_multiply_by_mu_length
                                    | OPTION_ANA_SIMULATE_CONFIG_reset
                                    | OPTION_ANA_SIMULATE_CONFIG_act
                                    | OPTION_ANA_SIMULATE_CONFIG_loop }
// ***
OPTION_ANA_SIMULATE_CONFIG_decl = { "[" ~ OPTION_ANA_SIMULATE_CONFIG_opt ~ ("," ~ OPTION_ANA_SIMULATE_CONFIG_opt)* ~ (",")? ~ "]" }
// ***
OPTION_ANA_KIND_simulate = { "simulate" ~ OPTION_ANA_SIMULATE_CONFIG_decl? }
OPTION_ANALYSIS_KIND = _{ OPTION_ANA_KIND_accept
                          | OPTION_ANA_KIND_prefix
                          | OPTION_ANA_KIND_eliminate
                          | OPTION_ANA_KIND_simulate }
OPTION_ANALYSIS_KIND_DECL = { "analysis_kind" ~ "=" ~ OPTION_ANALYSIS_KIND }
// ***********************************************
OPTION_GOAL_pass = { "Pass" }
OPTION_GOAL_weakpass = { "WeakPass" }
OPTION_GOAL_KIND = _{ OPTION_GOAL_pass | OPTION_GOAL_weakpass | HIBOU_none }
OPTION_GOAL_DECL = { "goal" ~ "=" ~ OPTION_GOAL_KIND }
// ***********************************************
OPTION_LOCANA_depth = { ("max_look_ahead_depth" | "max"~"depth" | "lookahead" | "lookup") ~ "=" ~ (HIBOU_none | ARITH_INTEGER)}
OPTION_LOCANA_modulo = { ("modulo" | "modulo_each_X_steps") ~ "=" ~ ARITH_INTEGER}
OPTION_LOCANA_select_all = { "all" | "All" | "ALL" }
OPTION_LOCANA_select_dirty = { "dirty" | "Dirty" | "DIRTY" }
OPTION_LOCANA_select = { "select" ~ "=" ~ (OPTION_LOCANA_select_all|OPTION_LOCANA_select_dirty) }
OPTION_LOCANA_CONFIG_opt = _{ OPTION_LOCANA_select
                                    | OPTION_LOCANA_depth
                                    | OPTION_LOCANA_modulo }
OPTION_LOCANA_CONFIG_decl = { "[" ~ OPTION_LOCANA_CONFIG_opt ~ ("," ~ OPTION_LOCANA_CONFIG_opt)* ~ (",")? ~ "]" }
OPTION_LOCANA = { "local_analysis" ~ "=" ~ (HIBOU_none|OPTION_LOCANA_CONFIG_decl) }
// ***********************************************
OPTION_PARTIAL_ORDER = { ("partial_order_reduction"|"por"|"POR") ~ "=" ~ (HIBOU_true|HIBOU_false) }
// ***********************************************
ANALYZE_OPTION_DECL = _{ GENERAL_OPTION_DECL
                            | OPTION_ANALYSIS_KIND_DECL
                            | OPTION_GOAL_DECL
                            | OPTION_LOCANA
                            | OPTION_PARTIAL_ORDER  }
ANALYZE_OPTION_SECTION =  { "@analyze_option" ~ "{" ~
                               	ANALYZE_OPTION_DECL ~ (";" ~ ANALYZE_OPTION_DECL)* ~ (";")?
                                   ~ "}"}
// ***********************************************




OPTION_CANON_searchall_yes = { "search_all" ~ ("=" ~ HIBOU_true)? }
OPTION_CANON_searchall_no = { "search_all" ~ "=" ~ HIBOU_false }
OPTION_CANON_searchall = _{ OPTION_CANON_searchall_yes | OPTION_CANON_searchall_no }
// ***
CANONIZE_OPTION_DECL = _{ GENERAL_OPTION_DECL
                            | OPTION_CANON_searchall }
CANONIZE_OPTION_SECTION =  { "@canonize_option" ~ "{" ~
                               	CANONIZE_OPTION_DECL ~ (";" ~ CANONIZE_OPTION_DECL)* ~ (";")?
                                   ~ "}"}


// ***********************************************

CONFIGURATION_SECTION = _{ EXPLORE_OPTION_SECTION
                            | ANALYZE_OPTION_SECTION
                            | CANONIZE_OPTION_SECTION }

HIBOU_CONFIGURATION = { CONFIGURATION_SECTION* }

HCF_PEST_FILE = _{ SOI ~ HIBOU_CONFIGURATION ~ EOI }
